MODULE main 
	VAR 
		pacman : agent(0,4,FALSE,gameover);
		ghost : agent(2,2,TRUE,gameover);
		
		sq1 : square(pacman, ghost,0, 1 ,FALSE ,TRUE ,TRUE ,FALSE );
		sq2 : square(pacman, ghost,1, 1 ,TRUE ,TRUE ,FALSE ,FALSE );
		sq3 : square(pacman, ghost,2, 1 ,FALSE ,TRUE ,FALSE ,TRUE );
		sq4 : square(pacman, ghost,0, 2 ,FALSE ,FALSE ,TRUE ,TRUE );
		sq5 : square(pacman, ghost,2, 2 ,FALSE ,FALSE ,TRUE ,TRUE );
		sq6 : square(pacman, ghost,0, 3 ,FALSE ,FALSE ,TRUE ,FALSE );
		sq7 : square(pacman, ghost,1, 3 ,FALSE ,TRUE ,FALSE ,FALSE );
		sq8 : square(pacman, ghost,2, 3 ,TRUE ,FALSE ,FALSE ,TRUE );
		sq9 : square(pacman, ghost,0, 4 ,TRUE ,FALSE ,TRUE ,FALSE );
		sq10 : square(pacman, ghost,1,4 ,TRUE ,FALSE ,FALSE ,TRUE );
						
		gameState : {WIN,LOSE,playing};
		gameover: boolean;
		
		time : 0..10;
	ASSIGN
		init(gameState) :=	case	is_colliding : LOSE;
									!has_gold : WIN;
									TRUE : playing;
							esac;
		next(gameState) :=	case	gameState != playing : gameState;
									next(is_colliding) : LOSE;
									is_crossing : LOSE;
									!next(has_gold) : WIN;
									TRUE : playing;
							esac;
		init(time) := 0;
		next(time) := case	time < 10 : time + 1;
							TRUE : time;
					esac;
	DEFINE
		-- Check if any square still has gold
		has_gold := sq1.gold | sq2.gold | sq3.gold | sq4.gold | sq5.gold | sq6.gold | sq7.gold | sq8.gold | sq9.gold | sq10.gold;
		-- Check if Pacman is at same position as ghost
		is_colliding := pacman.x = ghost.x & pacman.y = ghost.y;
		-- Check if Pacman crosses the ghost
		is_crossing :=	pacman.x = next(ghost.x) & pacman.y = next(ghost.y)
						& ghost.x = next(pacman.x) & ghost.y = next(pacman.y);
	INVAR
		-- gameover is just derived from gameState
		gameover = (gameState != playing);
	
	-- Vanuit elke begintoestand kan het spel zowel gewonnen als verloren worden.
	SPEC
		(EF gameState = WIN) & (EF gameState = LOSE);
	
	-- Muntjes kunnen niet terug verschijnen: als er ergens geen muntje ligt, dan
	-- zal in de toekomst er nooit nog een muntje liggen. (check dit voor 1 concreet vakje)
	LTLSPEC
		G (!sq1.gold -> G !sq1.gold);
	
	-- Het is mogelijk dat pacman oneindig vaak op locatie (1, 1) is terwijl het
	-- spel nog bezig is.
	SPEC
		EF (EG AF (pacman.x = 1 & pacman.y = 1 & !gameover));
	
	-- Als pacman oneindig vaak op locatie (1, 1) komt en oneindig vaak op
	-- locatie (1, 3) komt, dan wordt het spel nooit gewonnen.
	LTLSPEC
		G F (pacman.x = 1 & pacman.y = 1) & G F (pacman.x = 1 & pacman.y = 3) -> G gameState != WIN;
	
	-- Eens het spel verloren of gewonnen is, blijft het spel in deze toestand.
	LTLSPEC
		G (gameState = WIN -> G gameState = WIN) & G (gameState = LOSE -> G gameState = LOSE);
	
	-- Er zijn minstens 10 tijdstappen nodig om het spel te winnen.
	LTLSPEC
		-- Noot: weak-until is eigenlijk voldoende, time hoeft niet 10 te worden (mag vroeger stoppen)
		--       NuSMV heeft zelf geen weak-until en we weten dat time altijd 10 zal worden,
		--       dus het is niet echt nodig om zelf de equivalentie voor weak-until i.f.v. U en G te gebruiken
		(gameState != WIN) U (time = 10);


--Represents a "walking object"
-- it has a startposition (given by startX and startY), 
-- a boolean isGhost that is true if this agent is a ghost, 
-- and it knows whether the game is over (boolean gameover)
MODULE agent(startX,startY,isGhost,gameover)
	--TODO finish this module
	VAR
		x : 0..4;
		y : 0..4;
		move : { stop, up, down, left, right };
	ASSIGN
		init(x) := startX;
		init(y) := startY;
		next(x) :=	case	move = left & x > 0 : x - 1;
							move = right & x < 4 : x + 1;
							TRUE : x;
					esac;
		next(y) :=	case	move = down & y > 0 : y - 1;
							move = up & y < 4 : y + 1;
							TRUE : y;
					esac;
	INVAR
		-- Stop moving when game is over
		(move = stop	<-> gameover);
	TRANS
		-- Ghosts cannot switch direction in one turn
		isGhost ->
			(move = left	-> next(move) != right)
			& (move = right	-> next(move) != left)
			& (move = down	-> next(move) != up)
			& (move = up	-> next(move) != down);


--Represents a square
--u, d, l and r are booleans representing whether or not there is a wall in that specific direction from the square
--pacman and ghost are the unique player and ghost 
--x and y are its coordinates on the square
MODULE square(pacman,ghost,x,y,u,d,l,r)
	-- TODO do something here, probably related to coins ;-)
	VAR
		gold : boolean;
	ASSIGN
		init(gold) := TRUE;
		-- Gold disappears when pacman moves to the square
		-- Otherwise, the gold remains
		next(gold) :=	case	(next(pacman.x) = x & next(pacman.y) = y) : FALSE;
								TRUE : gold;
						esac;
	INVAR
		-- If Pacman is on this square, restrict his movement depending on walls
		(pacman.x = x & pacman.y = y) -> (
			(pacman.move = left		-> !l)
			& (pacman.move = right	-> !r)
			& (pacman.move = down	-> !d)
			& (pacman.move = up		-> !u)
		);
	INVAR
		-- Same for ghost
		(ghost.x = x & ghost.y = y) -> (
			(ghost.move = left		-> !l)
			& (ghost.move = right	-> !r)
			& (ghost.move = down	-> !d)
			& (ghost.move = up		-> !u)
		);
