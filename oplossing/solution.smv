MODULE main 
	VAR 
		pacman : agent(0,4,FALSE,gameover);
		ghost : agent(2,2,TRUE,gameover);
		
		sq1 : square(pacman, ghost,0, 1 ,FALSE ,TRUE ,TRUE ,FALSE );
		sq2 : square(pacman, ghost,1, 1 ,TRUE ,TRUE ,FALSE ,FALSE );
		sq3 : square(pacman, ghost,2, 1 ,FALSE ,TRUE ,FALSE ,TRUE );
		sq4 : square(pacman, ghost,0, 2 ,FALSE ,FALSE ,TRUE ,TRUE );
		sq5 : square(pacman, ghost,2, 2 ,FALSE ,FALSE ,TRUE ,TRUE );
		sq6 : square(pacman, ghost,0, 3 ,FALSE ,FALSE ,TRUE ,FALSE );
		sq7 : square(pacman, ghost,1, 3 ,FALSE ,TRUE ,FALSE ,FALSE );
		sq8 : square(pacman, ghost,2, 3 ,TRUE ,FALSE ,FALSE ,TRUE );
		sq9 : square(pacman, ghost,0, 4 ,TRUE ,FALSE ,TRUE ,FALSE );
		sq10 : square(pacman, ghost,1,4 ,TRUE ,FALSE ,FALSE ,TRUE );
						
		gameState : {WIN,LOSE,playing};
		gameover: boolean;
	--TODO finish this module
	ASSIGN
		init(gameState) :=	case	is_colliding : LOSE;
									!has_gold : WIN;
									TRUE : playing;
							esac;
		next(gameState) :=	case	gameState != playing : gameState;
									next(is_colliding) : LOSE;
									is_crossing : LOSE;
									!next(has_gold) : WIN;
									TRUE : playing;
							esac;
	DEFINE
		-- Check if any square still has gold
		has_gold := sq1.gold | sq2.gold | sq3.gold | sq4.gold | sq5.gold | sq6.gold | sq7.gold | sq8.gold | sq9.gold | sq10.gold;
		-- Check if Pacman is at same position as ghost
		is_colliding := pacman.x = ghost.x & pacman.y = ghost.y;
		-- Check if Pacman crosses the ghost
		is_crossing :=	pacman.x = next(ghost.x) & pacman.y = next(ghost.y)
						& ghost.x = next(pacman.x) & ghost.y = next(pacman.y);
	INVAR
		-- gameover is just derived from gameState
		gameover = (gameState != playing);

--Represents a "walking object"
-- it has a startposition (given by startX and startY), 
-- a boolean isGhost that is true if this agent is a ghost, 
-- and it knows whether the game is over (boolean gameover)
MODULE agent(startX,startY,isGhost,gameover)
	--TODO finish this module
	VAR
		x : 0..4;
		y : 0..4;
		u : boolean;
		d : boolean;
		l : boolean;
		r : boolean;
		move : { stop, up, down, left, right };
	ASSIGN
		init(x) := startX;
		init(y) := startY;
		next(x) :=	case	move = left & x > 0 : x - 1;
							move = right & x < 4 : x + 1;
							TRUE : x;
					esac;
		next(y) :=	case	move = down & y > 0 : y - 1;
							move = up & y < 4 : y + 1;
							TRUE : y;
					esac;
	INVAR
		-- Stop moving when game is over
		(move = stop	<-> gameover);
	INVAR
		-- Allow move if no wall in that direction 
		(move = left	-> !l)
		& (move = right	-> !r)
		& (move = down	-> !d)
		& (move = up	-> !u);
	TRANS
		-- Ghosts cannot switch direction in one turn
		isGhost ->
			(move = left	-> next(move) != right)
			& (move = right	-> next(move) != left)
			& (move = down	-> next(move) != up)
			& (move = up	-> next(move) != down);


--Represents a square
--u, d, l and r are booleans representing whether or not there is a wall in that specific direction from the square
--pacman and ghost are the unique player and ghost 
--x and y are its coordinates on the square
MODULE square(pacman,ghost,x,y,u,d,l,r)
	-- TODO do something here, probably related to coins ;-)
	VAR
		gold : boolean;
	ASSIGN
		init(gold) := TRUE;
		-- Gold disappears when pacman moves to the square
		-- Otherwise, the gold remains
		next(gold) :=	case	(next(pacman.x) = x & next(pacman.y) = y) : FALSE;
								TRUE : gold;
						esac;
	INVAR
		-- If agent is on this square, inform him about walls
		-- TODO Alternative: reason directly about agent's move?
		((pacman.x = x & pacman.y = y) -> (pacman.u = u & pacman.d = d & pacman.l = l & pacman.r = r))
		& ((ghost.x = x & ghost.y = y) -> (ghost.u = u & ghost.d = d & ghost.l = l & ghost.r = r));
