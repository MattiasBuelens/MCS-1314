/**
 * Title: An IDP source file for pacman
 */

vocabulary PacmanVoc {
	
	type time isa int
	/*
	 * Only use Start and Next in your theories,
	 * never hardcode integer timepoints!
	 *
	 */
	Start: time
	partial Next(time):time
	
	/*
	 * We guarantee you that dir, Left, Right, Up, Down
	 * will always be correctly interpreted in structures.
	 * You should not check this.
	 */
	type dir
	Down: dir
	Left: dir
	Right: dir
	Up: dir

	type xCo isa int
	type yCo isa int

	// The ghosts and pacman
	type agent 
	pacman:agent

	NoPos(xCo,yCo) 
	Wall(xCo,yCo,dir)

	Gold(time, xCo, yCo)
	GameLost(time)
	GameWon(time)
	Position(time, agent, xCo, yCo)
	
	Move(time, agent, dir)
	
	I_gold(xCo, yCo)				//Initial gold location
	I_position(agent, xCo, yCo)		//Initial positions of agents

	I_gameLost
	I_gameWon

	Cn_gold(time, xCo, yCo)
	C_gameLost(time)
	C_gameWon(time)
	C_position(time, agent, xCo, yCo)

	// Hulppredicaten
	RefPos(xCo,yCo)
	NextTo(xCo,yCo,dir,xCo,yCo)
	OpposingDir(dir,dir)
	WallSymm(xCo,yCo,dir)
	Connected(xCo,yCo,xCo,yCo)
	Reachable(xCo,yCo,xCo,yCo)
}

theory TimeTheory : PacmanVoc {
	{
		Start = MIN[:time].
		!t: Next(t) = t+1 <- time(t+1).
	}
}

theory PacmanTheory : PacmanVoc {

	// Geeft aan of posities naast elkaar liggen
	// (x2,y2) ligt naast (x1,y1) in de (gegeven) richting
	{
		! x1[xCo], y1[yCo], x2[xCo], y2[yCo] :
			NextTo(x1,y1,Up,x2,y2)		<- x2 = x1 & y2 = y1 + 1.
		! x1[xCo], y1[yCo], x2[xCo], y2[yCo] :
			NextTo(x1,y1,Down,x2,y2)	<- x2 = x1 & y2 = y1 - 1.
		! x1[xCo], y1[yCo], x2[xCo], y2[yCo] :
			NextTo(x1,y1,Right,x2,y2)	<- x2 = x1 + 1 & y2 = y1.
		! x1[xCo], y1[yCo], x2[xCo], y2[yCo] :
			NextTo(x1,y1,Left,x2,y2)	<- x2 = x1 - 1 & y2 = y1.
	}

	// Geeft de overstaande richting
	{
		OpposingDir(Up,Down).
		OpposingDir(Left,Right).
		! d1[dir], d2[dir] :
			OpposingDir(d1,d2) <- OpposingDir(d2,d1).
	}
	
	// Geeft aan of er een muur staat op een vak in de gegeven richting
	{
		// Plaats muren in beide richtingen
		! x[xCo], y[yCo], d[dir] : WallSymm(x,y,d) <- Wall(x,y,d).
		! x1[xCo], y1[yCo], d1[dir], x2[xCo], y2[yCo], d2[dir] :
			WallSymm(x2,y2,d2) <- Wall(x1,y1,d1) & OpposingDir(d1,d2) & NextTo(x1,y1,d1,x2,y2).
		// Plaats muren rond ongeldige vakjes
		! x1[xCo], y1[yCo], d[dir], x2[xCo], y2[yCo] :
			WallSymm(x1,y1,d) <- ~NoPos(x1,y1) & NoPos(x2,y2) & NextTo(x1,y1,d,x2,y2).
		// Plaats muren rond speelveld
		! x1[xCo], y1[yCo], d[dir] :
			WallSymm(x1,y1,d) <- ~NoPos(x1,y1) & ~(? x2[xCo], y2[yCo] : NextTo(x1,y1,d,x2,y2)).
	}

	// Geeft aan of twee buurvakjes verbonden zijn
	! x1[xCo], y1[yCo], x2[xCo], y2[yCo] :
		Connected(x1,y1,x2,y2) <=>
			~NoPos(x1,y1) & ~NoPos(x2,y2) & (? d[dir] : NextTo(x1,y1,d,x2,y2) & ~WallSymm(x1,y1,d)).

	// Selecteer een referentievakje van waaruit bereikbaarheid kan uitgedrukt worden
	! x[xCo], y[yCo] : RefPos(x,y) <=> ~NoPos(x,y)
		& (! x1[xCo], y1[yCo] : x1 < x | (x1 = x & y1 < y) => NoPos(x1, y1)).

	// Geeft aan of een vakje verbonden is met het referentievakje
	{
		! x1[xCo], y1[yCo], x2[xCo], y2[yCo] :
			Reachable(x1,y1,x2,y2) <- RefPos(x1,y1) & Connected(x1,y1,x2,y2).
		! x1[xCo], y1[yCo], x2[xCo], y2[yCo] :
			Reachable(x1,y1,x2,y2) <- (? x3[xCo], y3[yCo] : Connected(x3,y3,x2,y2) & Reachable(x1,y1,x3,y3)).
	}

	// Alle geldige vakjes zijn verbonden met het referentievakje
	! x1[xCo], y1[yCo], x2[xCo], y2[yCo] :
		RefPos(x1, y1) => ((x1 = x2 & y1 = y2) | NoPos(x2,y2) | Reachable(x1,y1,x2,y2)).

	/*
	 * LTC
	 */

	// Successor State Action Axiom & Inertia
	{
		! x[xCo], y[yCo] : Gold(Start,x,y) <- I_gold(x,y).
		! t[time], x[xCo], y[yCo] : Gold(Next(t),x,y) <- Gold(t,x,y) & ~Cn_gold(t,x,y).
	}

	{
		GameLost(Start) <- I_gameLost.
		// Spel kan verloren worden als het nog niet gewonnen is
		! t[time] : GameLost(Next(t)) <- GameLost(t) | (~GameWon(t) & C_gameLost(t)).
	}

	{
		GameWon(Start) <- I_gameWon.
		// Spel kan gewonnen worden als het nog niet verloren is of wordt
		! t[time] : GameWon(Next(t)) <- GameWon(t) | (~GameLost(t) & ~C_gameLost(t) & C_gameWon(t)).
	}

	{
		! a[agent], x[xCo], y[yCo] : Position(Start,a,x,y) <- I_position(a,x,y).
		! t[time], a[agent], x[xCo], y[yCo] : Position(Next(t),a,x,y) <- C_position(t,a,x,y).
		! t[time], a[agent], x[xCo], y[yCo] : Position(Next(t),a,x,y) <-
			Position(t,a,x,y) & (! x1[xCo], y1[yCo] : C_position(t,a,x1,y1) => x1 = x & y1 = y).
	}

	// Positie verandert wanneer agent beweegt
	! t[time], a[agent], xn[xCo], yn[yCo] :
		C_position(t,a,xn,yn) <=> ? d[dir], x[xCo], y[yCo] :
			Move(t,a,d) & Position(t,a,x,y) & NextTo(x,y,d,xn,yn).

	// Goud verdwijnt als Pacman op die positie staat (en het spel nog niet verloren is)
	! t[time], x[xCo], y[yCo] :
		Cn_gold(t,x,y) <=> ~GameLost(t) & Gold(t,x,y) & Position(t,pacman,x,y).

	// Spel initieel verloren als Pacman en spook op zelfde plaats staan
	I_gameLost <=> ? a[agent], x[xCo], y[yCo]:
		a ~= pacman & Position(Start,a,x,y) & Position(Start,pacman,x,y).

	{
		// Spel verloren als Pacman en spook naar zelfde plaats gaan
		! t[time] : C_gameLost(t) <- ? a[agent], x[xCo], y[yCo]:
			a ~= pacman & C_position(t,a,x,y) & C_position(t,pacman,x,y).

		// Spel verloren als Pacman en spook elkaar kruisen
		! t[time] : C_gameLost(t) <- ? a[agent], xa[xCo], ya[yCo], xp[xCo], yp[yCo]:
			a ~= pacman & Position(t,a,xa,ya) & Position(t,pacman,xp,yp)
				& C_position(t,a,xp,yp) & C_position(t,pacman,xa,ya).
	}

	// Spel initieel gewonnen als er geen goud is (en het spel niet verloren is)
	I_gameWon <=> ~GameLost(Start) & ~(? x[xCo], y[yCo] : Gold(Start,x,y)).

	{
		// Spel gewonnen als er geen goud meer is (en het spel nog niet verloren is)
		// => Spel zal gewonnen worden als Pacman het laatste goudstuk gaat opnemen
		! t[time] : C_gameWon(t) <- ! x[xCo], y[yCo] :
			Gold(t,x,y) => C_position(t,pacman,x,y).
		// Spel gewonnen als een spook zich vastloopt in een doodlopend stuk (en het spel nog niet verloren is)
		// => Spel zal gewonnen worden als een spook zich gaat vastlopen
		! t[time] : C_gameWon(t) <- ? a[agent], x[xCo], y[yCo] :
			a ~= pacman & C_position(t,a,x,y) & ?3 d[dir] : WallSymm(x,y,d).
	}

	// Iedereen is op exact één plaats
	! t[time], a[agent] : ?1 x[xCo], y[yCo] : Position(t,a,x,y).

	// Agenten moet op geldige posities staan
	! t[time], a[agent], x[xCo], y[yCo] : Position(t,a,x,y) => ~NoPos(x,y).

	// Goud moet op geldige posities liggen
	! t[time], x[xCo], y[yCo] : Gold(t,x,y) => ~NoPos(x, y).

	// Niemand beweegt door muren
	! t[time], a[agent], d[dir], x[xCo], y[yCo] :
		Move(t,a,d) & Position(t,a,x,y) => ~WallSymm(x,y,d).

	// Niemand mag bewegen als het spel gedaan is
	! t[time] : GameWon(t) | GameLost(t) => ~? a[agent], d[dir] : Move(t,a,d).

	// Iedereen moet bewegen als het spel nog niet gedaan is
	// + Iedereen kan hoogstens in één richting tegelijk bewegen
	! t[time] : ~(GameWon(t) | GameLost(t)) => ! a[agent] : ?1 d[dir] : Move(t,a,d).

	// Niemand beweegt van het bord
	// Reeds afgehandeld, muren rond bord

	// Spoken mogen zich niet in één stap omkeren
	! t[time], a[agent], d1[dir], d2[dir] :
		a ~= pacman & Move(t,a,d1) & OpposingDir(d1,d2) => ~Move(Next(t),a,d2).
}

theory verification1: PacmanVoc {
	//Verify something here
}

procedure verification1proc(struc) {
	//Do something here: verify verification1 in the context of structure struc. 
	//At least print whether the verification is satisfied or not.
}

theory verification2: PacmanVoc {
	//Verify something here
}

procedure verification2proc(struc) {
	//Do something here: verify verification2 in the context of structure struc. 
	//At least print whether the verification is satisfied or not.
}

theory verification3: PacmanVoc {
	//Verify something here
}

procedure verification3proc(struc) {
	//Do something here: verify verification3 in the context of structure struc. 
	//At least print whether the verification is satisfied or not.
}

theory verification4: PacmanVoc {
	//Verify something here
}

procedure verification4proc(struc) {
	//Do something here: verify verification4 in the context of structure struc. 
	//At least print whether the verification is satisfied or not.
}

procedure allVerifications() {
	strucToCheck = verStruc
	verification1proc(strucToCheck)
	verification2proc(strucToCheck)
	verification3proc(strucToCheck)
	verification4proc(strucToCheck)
}

structure verStruc: PacmanVoc {
	
	time = {0..50}
	dir = {u;d;l;r}
	Up = u
	Down = d
	Left = l
	Right = r

	xCo = {1..5}
	yCo = {1..5}

	pacman = p
	agent = {p; blinky}

	NoPos = {1,1; 1,2;  4,2; 4,3}
	I_position = {p,1,3; blinky, 4,4}

	Wall = {2,2,u;2,3,r;3,2,d;4,4,u;1,4,r;2,4,u}

	I_gold<cf> = {1,1; 1,2; 4,2; 4,3; 5,5}
	I_gold<u> = {}

}
